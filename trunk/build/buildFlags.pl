#!/usr/bin/perl

#
# Copyright (c) 2008 James Molloy, James Pritchett, Jörg Pfähler, Matthew Iselin
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
#

# Parses a file called "buildFlags".
# * Any line beginning with # is a comment.
# * Any line of the form <identifier>: <identifier>+ is interpreted as "<left identifier> requires <right identifiers>". If <left identifier> is found to be set, <right identifiers> are also set.
# * Any line starting with [<username>] is only applicable if the script is being run as user <username>.

my $username = `whoami`;
chomp $username;

open my $r, "<", "buildFlags";
open my $w, ">", "buildFlags.mk";

my %rules;
my %flags;

while (<$r>) {
  next if $_ =~ m/^#/; # Detect comments.
  next if $_ =~ m/^[ \t]*$/; # Detect empty lines.
  
  my $line = $_;
  if ($line =~ m/^\[(.*)\][ \t]*(.*)$/) {
    # user specific line.
    $line = $2;
    next unless ($1 eq $username);
  }
  
  if ($line =~ m/^(.+): (.*)$/) {
    # rule line.
    $rules{$1} = $2;
  } else {
    # definition line.
    my ($flag) = $line =~ m/[ \t]*(.+)[ \t]*$/;
    $flags{$flag} = 1;
  }
}

# Apply rules.
my $applied = 1;
while ($applied) {
  $applied = 0;
  foreach my $flag (keys %flags) {
    if (defined $rules{$flag}) {
      # Apply this rule.
      foreach (split (/ +/, $rules{$flag})) {
        $flags{$_} = 1;
      }
      $applied = 1;
      $rules{$flag} = undef;
    }
  }
}

print $w "# This file was autogenerated by buildFlags.pl. DO NOT EDIT!\n";

my $cmd_flags = "";
foreach my $key (keys %flags) {
  print $w "export $key=".$flags{$key}."\n";
  $cmd_flags .= "-D$key=".$flags{$key}." ";
}
print $w "CMD_FLAGS += $cmd_flags\n";

